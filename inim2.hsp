//ここからモジュールの開始です。
;┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━
;┃ [ ini module v2]
;┃
;┃ iniファイルを扱います
;┃
;┃ 主な命令
;┃ INIモジュール初期化「INIInit p1」（p1 INIのパスを指定）
;┃ INIから読み込む「INIGet」（p1 セクション名,p2 キー名）【ctype】
;┃ INIを書き換える「INISet」（p1 セクション名,p2 キー名,p3 書き換える文字列）[""で削除]
;┃
;┃ 2006/12/28 公開
;┃ 2008/04/04 ver2
;┃ 	INIGetで、キーが存在しない場合の判定を行うようにしました。
;┃ 	INIGetで、ファイルが存在しない場合を判定するようにしました。
;┃
;┃[ placeark ]
;┃ by natade
;┃ url http://www.md.ccnw.ne.jp/ark/
;┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━

#module "inimodule"
#uselib "kernel32"
#cfunc WritePrivateProfileString "WritePrivateProfileStringA" sptr,sptr,sptr,sptr
#cfunc GetPrivateProfileString "GetPrivateProfileStringA" sptr,sptr,sptr,sptr,sptr,sptr
#cfunc GetFullPathName "GetFullPathNameA" sptr,sptr,sptr,sptr
#define MAX_PATH 512
#define NULL 0
#define nSize 256
;--------INIInit(INIモジュール初期化)---------
;p1 INIファイルのパス
#deffunc INIInit str inii_s
	sdim INI_FileName,strlen(inii_s):INI_FileName=inii_s
	sdim INI_LongName,MAX_PATH
	exist inii_s:ini_filesize = strsize
	inii_size = GetFullPathName(varptr(INI_FileName),MAX_PATH,varptr(INI_LongName),NULL)
	return inii_size
;--------INIGet(INIから読み込む)---------
;p1 セクション名
;p2 キー名
;refstr 取得した文字列
;戻り値　取得したサイズ
#defcfunc INIGet str inig_section,str inig_key
	mref inig_stat,64
	mref inig_refstr,65
	if(ini_filesize = -1){return 0}
	sdim INI_Section,strlen(inig_section):INI_Section=inig_section
	sdim INI_Key,strlen(inig_key):INI_Key=inig_key
	sdim lpReturnedString,nSize
	inig_stat   = GetPrivateProfileString(varptr(INI_Section),varptr(INI_Key),NULL,varptr(lpReturnedString),nSize,varptr(INI_LongName))
	inig_refstr = lpReturnedString
	return inig_stat
;--------INISet(INIを書き換える)---------
;p1 セクション名
;p2 キー名（""でセクションを削除）
;p3 書き換える文字列（""でキーを削除）
#deffunc INISet str inis_section,str inis_key,str inis_str
	sdim INI_Section,strlen(inis_section):INI_Section=inis_section
	if (inis_key!=""){sdim INI_Key,strlen(inis_key):INI_Key=inis_key:INI_KeyPtr=varptr(INI_Key)}else{INI_KeyPtr=NULL}
	if (inis_str!=""){sdim INI_String,strlen(inis_str):INI_String=inis_str:INI_StringPtr=varptr(INI_String)}else{INI_StringPtr=NULL}
	ret = WritePrivateProfileString(varptr(INI_Section),INI_KeyPtr,INI_StringPtr,varptr(INI_LongName))
	return
#global
